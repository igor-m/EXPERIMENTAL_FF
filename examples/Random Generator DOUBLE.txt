\ RANDOM NUMBER GENERATOR
\ based on http://www.taygeta.com/fsl/library/r250.seq
\ Copyright 1994  Everett F. Carter 
\ mod for amforth IgorM 2010
\ No warranties of any kind
\ Provided as is

-rnd
marker -rnd
decimal

2147483647. 2constant max32

: ?dup  ( a -- a a | 0 ) dup if dup then ;
: mu* ( ud1 u--ud2 )  tuck * >r  um*  r> + ;
: um/ ( ud u--u )  um/mod nip ;
: mu/ ( ud u--ud )  >r  0 r@  um/mod  r> swap >r  um/  r> ;

: r_um*/ ( u1 u2 u3--ud )  >r um*  r> mu/ ;
: r_m*/  ( n1 n2 n3 -- n4 ) >r m* r> sm/rem nip ;

: +d/ ( +d1 +d2--+d3 )  ?dup if  dup 1+  0 1 rot  um/
                                dup >r  mu*
                                >r over swap r@ r_um*/ d-
                                r> r> swap r_m*/  nip 0             
                          else  mu/
                          then ;
: r_ud* ( ud ud--ud )  dup if  2swap  then  drop  mu* ;
: 4dup 3 pick 3 pick 3 pick 3 pick ;
: +d/mod ( +d1 +d2--+d3 +d4 ) 4dup  +d/  2dup swap >r >r  r_ud*  d-  r> r> swap ; 

2variable seed          

68623691. seed 2!

: rnd  ( -- d )  seed 2@ 127773. +d/mod
                         swap >r >r
                         16807 mu*
                         r> r> swap
                         2836 mu* d-
                         2dup d0< if max32 d+ then
                         2dup seed 2!
;






